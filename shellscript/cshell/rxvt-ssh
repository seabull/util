#!/bin/csh -f
#------------------------------------------------------------------------------

  if ( $#argv > 1 ) then
    set msg = "too many arguments"
    goto error
  endif

  if ( "$1" == "" ) then
    set msg = "missing <user>@<hostname> argument"
    goto error
  endif

  if ( "$1" !~ *@* ) then
    set msg = "<user>@<hostname> argument missing '@<hostname>'"
    goto error
  endif

  if ( "$1" =~ *@ ) then
    set msg = "<user>@<hostname> argument missing '<hostname>'"
    goto error
  endif

  if ( "$1" =~ @* ) then
    set msg = "<user>@<hostname> argument missing '<user>'"
    goto error
  endif

  set destination = "` echo $1 | sed -e 's/.*@//' `"

#------------------------------------------------------------------------------

  set path = ( /usr/local/bin /bin /usr/X11R6/bin $path )

  setenv PATH "` clean-exec-path `"

  setenv USER "`whoami`"

  if ( $?USER == 0 || "$USER" == "" ) then
    set usage = "unable to establish user identity"
    goto error
  endif

  run rxvt	-fn '-adobe-courier-medium-r-normal--12-*' \
		-fg 'light blue' -bg 'black' -tn vt100 \
		-T "$destination" \
		-e ssh "$1"
  exit 0

#------------------------------------------------------------------------------

error:

  echo ""
  echo "Error:  $msg"

usage:

  echo ""
  echo "Usage:  rxvt-ssh <user>@<hostname>"
  echo ""

  exit -1

#------------------------------------------------------------------------------
