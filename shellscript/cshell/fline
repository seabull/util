#!/bin/csh -f
#------------------------------------------------------------------------------

  if ( $#argv != 2 ) then
    set msg = "wrong number of arguments"
    goto usage
  endif

  set file = "$1"

  if ( "$file" == "" ) then
    set msg = "Invalid file name specified - empty string"
    goto usage
  endif

  if ( ! -f "$file" ) then
    set msg = "'$file' does not exist or is not a plain file."
    goto usage
  endif

  @ line = 0 + $2

  if ( $line <= 0 ) then
    set msg = "<line>, $2, must be greater than zero"
    goto usage
  endif

#------------------------------------------------------------------------------

  cat -n $file | head -$line | tail -1 | sed "s/\(^ *$line""[	]\) /&>/"

  exit 0

#------------------------------------------------------------------------------

usage:

  echo ""
  echo "error:  $msg"
  echo ""
  echo "usage:  near <file> <line>"
  echo ""

  exit -1

#------------------------------------------------------------------------------
